# Copyright 2021 Nikita Melekhin. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

lib_c_flags = [
  "-ffreestanding",
  "-Werror",
  "-Wno-address-of-packed-member",
]

if (userland_symbols) {
  lib_c_flags += [ "-ggdb" ]
}

if (optimize) {
  lib_c_flags += [ "-Os" ]
}

if (static_analyze) {
  lib_c_flags += [ "-fanalyzer" ]
}

lib_cc_flags = [
  "-std=c++2a",
  "-fno-sized-deallocation",
  "-fno-rtti",
  "-fno-exceptions",
  "-D_LIBCXX_BUILD_ONEOS_EXTENSIONS",
]

lib_asm_flags = []

if (target_cpu == "x86") {
  lib_asm_flags += [
    "-f",
    "elf",
  ]
}

if (target_cpu == "aarch32") {
  lib_c_flags += [
    "-fno-builtin",
    "-march=armv7-a+neon-vfpv4",
    "-mfpu=neon-vfpv4",
    "-mfloat-abi=softfp",
    "-fno-pie",
    "-no-pie",
  ]
  lib_asm_flags += [
    "-march=armv7-a+neon-vfpv4",
    "-mfpu=neon-vfpv4",
    "-mfloat-abi=softfp",
    "-mcpu=cortex-a15",
  ]
}

config("lib_flags") {
  cflags = lib_c_flags
  asmflags = lib_asm_flags
}

config("libcxx_flags") {
  cflags = lib_c_flags
  asmflags = lib_asm_flags
  cflags_cc = lib_cc_flags
}

group("libs") {
  deps = [
    "//libs/libc:libc",
    "//libs/libcxx:libcxx",
    "//libs/libfoundation:libfoundation",
    "//libs/libg:libg",
    "//libs/libui:libui",
  ]
}
